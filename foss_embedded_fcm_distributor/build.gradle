plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdk 34

    compileOptions{
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        // languageVersion provides source compatibility with the specified version of Kotlin
        languageVersion = '1.7'
    }

    defaultConfig {
        minSdkVersion 16
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig = false
    }

    namespace "org.unifiedpush.android.foss_embedded_fcm_distributor"
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

}

def releaseGroupId = 'org.unifiedpush.android'
def releaseArtifactId = 'foss-embedded-fcm-distributor'
def releaseVersion = '1.0.0'

tasks.register('printArtifactId') {
    group 'Artifact Info'
    doLast {
        println("artifact=$releaseGroupId:$releaseArtifactId")
    }
}

tasks.register("printVersion") {
    group 'Artifact Info'
    doLast {
        println("version=$releaseVersion")
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                pom {
                    name = "$releaseGroupId:$releaseArtifactId"
                    description = 'FOSS Embedded FCM Distributor'
                    url = 'https://unifiedpush.org/developers/embedded_fcm/'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'S1m'
                            email = 'git@sgougeon.fr'
                            organization = 'UnifiedPush'
                            organizationUrl = 'https://unifiedpush.org/'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://codeberg.org/UnifiedPush/android-foss_embedded_fcm_distributor.git'
                        developerConnection = 'scm:git:ssh://codeberg.org:UnifiedPush/android-foss_embedded_fcm_distributor.git'
                        url = 'https://codeberg.org/UnifiedPush/android-foss_embedded_fcm_distributor'
                    }
                }
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = releaseGroupId
                artifactId = releaseArtifactId
                version = releaseVersion
            }
        }
    }
}
